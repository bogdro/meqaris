#!/usr/bin/perl
#
# Copyright (C) 2022-2023 Bogdan 'bogdro' Drozdowski, bogdro (at) users . sourceforge . net
#
# This file is part of Meqaris (Meeting Equipment and Room Invitation System),
#  software that allows booking meeting rooms and other resources using
#  e-mail invitations.
# Meqaris homepage: https://meqaris.sourceforge.io/
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU Affero General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU Affero General Public License for more details.
#
#  You should have received a copy of the GNU Affero General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use warnings;

use Config::Simple;
use Data::ICal;
use DateTime::Format::ICal;
use DateTime::Set;
use DateTime::TimeZone::ICal;
use DBI;
use Fcntl;				# core
use Getopt::Long;			# core
use MIME::Base64 qw(encode_base64);	# core
use MIME::Parser;
use Term::ANSIColor qw(:constants);	# core (5.6.0)

my $meqaris_version = '1.4';
my $meqaris_dbversion = 7;

# ============================ Loggers: ===================================

my $logger = undef;
my $log_level = 0;

sub log_trace ($);
sub log_debug ($);
sub log_info ($);
sub log_warn ($);
sub log_error ($);
sub log_fatal ($);

eval {
	# some default configuration
	my $config_text = q(
		log4perl.rootLogger = WARN, Screen
		log4perl.appender.Screen = Log::Log4perl::Appender::Screen
		log4perl.appender.Screen.stderr = 1
		log4perl.appender.Screen.layout = Log::Log4perl::Layout::PatternLayout
		log4perl.appender.Screen.layout.ConversionPattern = [%d{yyyy-MM-dd HH:mm:ss}][PID=%P] %p - %m%n
		);
	use Log::Log4perl;
	Log::Log4perl->init (\$config_text);
	$logger = Log::Log4perl->get_logger('Meqaris');
};

# ============================ Command line processing =======================

sub send_mail ($$$@);
sub val ($);
sub clean_name ($);
sub prop_array ($$);
sub is_empty ($);
sub db_stmt ($$);
sub db_stmt_print ($$);
sub db_version ($);
sub db_run_file ($$);
sub db_upgrade ($);
sub db_upgrade_print ($);
sub dbunlock();
sub dblock();

log_trace 'Reading the command line.';

Getopt::Long::Configure('ignore_case', 'ignore_case_always');

my $config_file = '/etc/meqaris.ini';
my $create = '';
my $delete = '';
my $description = '';
my $destroy_db = '';
my $disable = '';
my $email = '';
my $enable = '';
my $help = '';
my $lic = '';
my $name = '';
my $set = '';
my $status = '';
my $update = '';
my $upgrade_db = '';
my $value = undef;
my $version = '';

if ( !GetOptions (
	'conf=s'		=> \$config_file,
	'create=s'		=> \$create,
	'delete=s'		=> \$delete,
	'description=s'		=> \$description,
	'destroy-db'		=> \$destroy_db,
	'disable=s'		=> \$disable,
	'email=s'		=> \$email,
	'enable=s'		=> \$enable,
	'h|help|?'		=> \$help,
	'license|licence'	=> \$lic,
	'name=s'		=> \$name,
	'set=s'			=> \$set,
	'status'		=> \$status,
	'update=s'		=> \$update,
	'upgrade-db'		=> \$upgrade_db,
	'value=s'		=> \$value,
	'version'		=> \$version,
	)
   ) {
	print_help();
	exit 1;
}

log_trace 'Processing command line arguments.';

if ( $lic ) {
	print
		"Meqaris (Meeting Equipment and Room Invitation System)\n" .
		" - a system that allows booking meeting rooms and other resources\n" .
		" using e-mail invitations. See https://meqaris.sourceforge.io/\n" .
		"Author: Bogdan 'bogdro' Drozdowski, bogdro (at) users . sourceforge . net.\n\n" .
		"  This program is free software: you can redistribute it and/or modify\n" .
		"  it under the terms of the GNU Affero General Public License as published by\n" .
		"  the Free Software Foundation, either version 3 of the License, or\n" .
		"  (at your option) any later version.\n\n" .
		"  This program is distributed in the hope that it will be useful,\n" .
		"  but WITHOUT ANY WARRANTY; without even the implied warranty of\n" .
		"  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n" .
		"  GNU Affero General Public License for more details.\n\n" .
		"  You should have received a copy of the GNU Affero General Public License\n" .
		"  along with this program.  If not, see <http://www.gnu.org/licenses/>.\n"
		;
	exit 0;
}

if ( $help ) {
	print_help();
	exit 0;
}

if ( $version ) {
	print "Meqaris version $meqaris_version\n";
	exit 0;
}

log_debug "Reading configuration file '$config_file'.";

my %inicfg;
Config::Simple->import_from($config_file, \%inicfg);

log_trace "Done reading configuration file '$config_file'.";

if ( ! is_empty ($inicfg{'meqaris.log4perl_config_location'}) ) {

	log_debug "Attempting to switch Log4perl to configuration file '"
		. $inicfg{'meqaris.log4perl_config_location'} . "'.";
	eval {
		use Log::Log4perl;
		Log::Log4perl->init ($inicfg{'meqaris.log4perl_config_location'});
		$logger = Log::Log4perl->get_logger('Meqaris');
	};
}

if ( $status ) {

	log_debug 'Command line: --status';

	my $error = 0;
	if ( Config::Simple->error() ) {
		print "Configuration file '$config_file': ", BOLD, RED, 'ERROR', RESET, ': '
			. Config::Simple->error() . "\n";
		$error = 1;
	} else {
		print "Configuration file '$config_file': ", BOLD, GREEN, 'OK', RESET, "\n";
	}

	if ( ! $error ) {

		for my $k ('meqaris.dbtype',
			'meqaris.datadir',
			'meqaris.log4perl_config_location',
			'meqaris.lock_dir',
			) {

			if ( ! defined ($inicfg{$k}) ) {
				print "Configuration data in file '$config_file': ",
					BOLD, RED, 'ERROR', RESET, ": Missing: $k\n";
				$error = 1;
			}
		}
		if ( defined ($inicfg{'meqaris.dbtype'}) ) {

			for my $k (
				"$inicfg{'meqaris.dbtype'}.username",
				"$inicfg{'meqaris.dbtype'}.password",
				"$inicfg{'meqaris.dbtype'}.dbname",
				"$inicfg{'meqaris.dbtype'}.host",
				"$inicfg{'meqaris.dbtype'}.port",
				"$inicfg{'meqaris.dbtype'}.connect_timeout"
				) {

				if ( ! defined ($inicfg{$k}) ) {
					print "Configuration data in file '$config_file': ",
						BOLD, RED, 'ERROR', RESET, ": Missing: $k\n";
					$error = 1;
				}
			}
		}
	}
	if ( ! $error ) {

		if ( is_empty ($inicfg{'meqaris.log4perl_config_location'})
			|| ! -f $inicfg{'meqaris.log4perl_config_location'} ) {

			print "Log4Perl configuration: ", BOLD, RED, 'ERROR', RESET, ': '
				. (defined($inicfg{'meqaris.log4perl_config_location'})?
					$inicfg{'meqaris.log4perl_config_location'} . ' is not a file.' :
					'[meqaris] log4perl_config_location <not defined>')
				. "\n";
			$error = 1;
		} else {
			print "Log4Perl configuration: '$inicfg{'meqaris.log4perl_config_location'}': ",
				BOLD, GREEN, 'OK', RESET, "\n";
		}
	}
	if ( ! $error ) {

		if ( ! -d "$inicfg{'meqaris.datadir'}/sql" ) {
			print "Data directory: ", BOLD, RED, 'ERROR', RESET,
				": '$inicfg{'meqaris.datadir'}/sql' is not a directory.\n";
			$error = 1;
		} else {
			print "Data directory: '$inicfg{'meqaris.datadir'}': ",
				BOLD, GREEN, 'OK', RESET, "\n";
		}
	}
	if ( ! $error ) {

		if ( ! -d "$inicfg{'meqaris.lock_dir'}" ) {
			print "Lock directory: ", BOLD, RED, 'ERROR', RESET,
				": '$inicfg{'meqaris.lock_dir'}' is not a directory.\n";
			$error = 1;
		} else {
			print "Lock directory: '$inicfg{'meqaris.lock_dir'}': ",
				BOLD, GREEN, 'OK', RESET, "\n";
		}
	}
	if ( ! $error ) {
		eval 'use DBD::Pg;'; 
		if ($@) {
			print 'Database connection: ', BOLD, RED, 'ERROR', RESET, ": Module DBD::Pg not installed\n";
			$error = 2;
		}
	}
	if ( ! $error ) {

		my $dbh = db_connect();
		if ( ! $dbh ) {
			print 'Database connection: ', BOLD, RED, 'ERROR', RESET, "\n";
			$error = 2;
		}
		if ( ! $error ) {
			print 'Database connection: ', BOLD, GREEN, 'OK', RESET, "\n";
			my $db_ver = db_version ($dbh);
			if ( $meqaris_dbversion == $db_ver ) {
				print 'Database version: ', BOLD, GREEN, 'OK', RESET, "\n";
			} else {
				print 'Database version: ', BOLD, RED, 'ERROR', RESET,
					": expected $meqaris_dbversion, got $db_ver\n" .
					"You may need to run 'meqaris --upgrade-db'.\n";
				$error = 2;
			}
			$dbh->disconnect;
		}
	}
	exit $error;
}

if ( $create ) {

	log_info "Command line: --create: name='$create' email='$email' desc='$description'";

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $stmth = db_stmt_print ($dbh, 'insert into meqaris.meq_resources(r_name, r_email, r_description) values ($1, $2, $3)');
	$create = clean_name ($create);
	my $rowcount = $stmth->execute (substr ($create, 0, 1000),
		substr ($email, 0, 1000), substr ($description, 0, 1000));
	if ( $dbh->errstr ) {
		log_error 'Database: cannot create resource: ' . $dbh->errstr;
		print 'Database: cannot create resource: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot create resource (unknown error).';
		print "Database: cannot create resource (unknown error).\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Resource '$create' added successfully.";
	print "Resource '$create' added successfully.\n";
	exit 0;
}

if ( $delete ) {

	log_info "Command line: --delete: name='$delete'";

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $stmth = db_stmt_print ($dbh, 'delete from meqaris.meq_resources where r_name = $1');
	$delete = clean_name ($delete);
	my $rowcount = $stmth->execute ($delete);
	if ( $dbh->errstr ) {
		log_error 'Database: cannot delete resource: ' . $dbh->errstr;
		print 'Database: cannot delete resource: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot delete resource - not found.';
		print "Database: cannot delete resource - not found.\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Resource '$delete' deleted successfully.";
	print "Resource '$delete' deleted successfully.\n";
	exit 0;
}

if ( $destroy_db ) {

	log_warn 'Command line: --destroy-db';

	my $dbh = db_connect_print();
	my $stmth = db_stmt_print ($dbh, 'drop schema meqaris cascade');
	$stmth->execute();
	if ( $dbh->errstr ) {
		log_error 'Database: cannot destroy the database schema: ' . $dbh->errstr;
		print 'Database: cannot destroy the database schema: ' . $dbh->errstr . "\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info 'Database schema deleted.';
	print "Database schema deleted.\n";
	exit 0;
}

if ( $disable ) {

	log_info "Command line: --disable: name='$disable'";

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $stmth = db_stmt_print ($dbh, 'update meqaris.meq_resources set r_enabled = false where r_name = $1');
	$disable = clean_name ($disable);
	my $rowcount = $stmth->execute ($disable);
	if ( $dbh->errstr ) {
		log_error 'Database: cannot disable resource: ' . $dbh->errstr;
		print 'Database: cannot disable resource: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot disable resource - not found.';
		print "Database: cannot disable resource - not found.\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Resource '$disable' disabled successfully.";
	print "Resource '$disable' disabled successfully.\n";
	exit 0;
}

if ( $enable ) {

	log_info "Command line: --enable: name='$enable'";

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $stmth = db_stmt_print ($dbh, 'update meqaris.meq_resources set r_enabled = true where r_name = $1');
	$enable = clean_name ($enable);
	my $rowcount = $stmth->execute ($enable);
	if ( $dbh->errstr ) {
		log_error 'Database: cannot enable resource: ' . $dbh->errstr;
		print 'Database: cannot enable resource: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot enable resource - not found.';
		print "Database: cannot enable resource - not found.\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Resource '$enable' enabled successfully.";
	print "Resource '$enable' enabled successfully.\n";
	exit 0;
}

if ( $update ) {

	log_info "Command line: --update: name='$name' email='$email' desc='$description'";
	if ( $name eq '' and $email eq '' and $description eq '' ) {

		log_error 'Neither a new name, a new e-mail address or a new description have been provided.';
		print "Neither a new name, a new e-mail address or a new description have been provided.\n";
		exit 1;
	}

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $sql = 'update meqaris.meq_resources set ';
	my $first = 1;
	my $par_cnt = 2;
	my @values = ($update);
	if ( $name ne '' ) {

		$name = clean_name($name);
		$sql .= "r_name = \$$par_cnt";
		push @values, substr ($name, 0, 1000);
		$first = 0;
		$par_cnt++;
	}
	if ( $email ne '' ) {

		if ( ! $first ) {
			$sql .= ', ';
		}
		$sql .= "r_email = \$$par_cnt";
		push @values, substr ($email, 0, 1000);
		$first = 0;
		$par_cnt++;
	}
	if ( $description ne '' ) {

		if ( ! $first ) {
			$sql .= ', ';
		}
		$sql .= "r_description = \$$par_cnt";
		push @values, substr ($description, 0, 1000);
		$first = 0;
		$par_cnt++;
	}

	$sql .= ' where r_name = $1';

	my $stmth = db_stmt_print ($dbh, $sql);
	my $rowcount = $stmth->execute (@values);
	if ( $dbh->errstr ) {
		log_error 'Database: cannot update resource: ' . $dbh->errstr;
		print 'Database: cannot update resource: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot enable resource - not found.';
		print "Database: cannot enable resource - not found.\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Resource '$update' updated successfully.";
	print "Resource '$update' updated successfully.\n";
	exit 0;
}

if ( $set ) {

	log_info "Command line: --set: name='$set'";
	if ( $set eq 'db_version' ) {

		log_debug 'Attempting to modify a forbidden parameter';
		exit 1;
	}
	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	my $stmth = db_stmt_print ($dbh, 'update meqaris.meq_config set c_value = $2 where c_name = $1');
	my $rowcount = $stmth->execute ($set, $value);
	if ( $dbh->errstr ) {
		log_error 'Database: cannot modify setting: ' . $dbh->errstr;
		print 'Database: cannot modify setting: ' . $dbh->errstr . "\n";
		exit 2;
	}
	if ( $rowcount < 1 ) {
		log_error 'Database: cannot enable setting - not found.';
		print "Database: cannot enable setting - not found.\n";
		exit 2;
	}
	$dbh->disconnect;
	log_info "Setting '$set' modified successfully.";
	print "Setting '$set' modified successfully.\n";
	exit 0;
}

if ( $upgrade_db ) {

	log_info 'Command line: --upgrade-db';

	my $dbh = db_connect_print();
	db_upgrade_print ($dbh);
	$dbh->disconnect;
	log_info 'Database upgraded successfully.';
	print "Database upgraded successfully.\n";
	exit 0;
}

# ============================ Input processing ===================================

log_trace 'Connecting to the database.';

my $dbh = db_connect();
if ( ! $dbh ) {

	dbunlock();
	exit 2;
}
log_trace 'Checking if database upgrade needed.';

if ( ! db_upgrade ($dbh) ) {

	dbunlock();
	exit 3;
}

log_trace 'Reading configuration from the database.';

my $dbcfg = $dbh->selectall_hashref (db_stmt ($dbh, 
	"select c_name, c_value from meqaris.meq_config where c_name <> 'db_version'"), 'c_name');

if ( ! $dbcfg ) {

	log_warn 'Cannot read configuration from the database - using defaults.'
		. " Database error:\n" . $dbh->errstr;
}

my $parser_tmp_dir = '/tmp';
if ( defined ($dbcfg->{'parser_tmp_dir'}) ) {

	$parser_tmp_dir = $dbcfg->{'parser_tmp_dir'}->{'c_value'};
	if ( is_empty ($parser_tmp_dir) || ! -d $parser_tmp_dir ) {

		log_warn "Parser directory '$parser_tmp_dir' is not set or not a directory - using defaults.";
		$parser_tmp_dir = '/tmp';
	}
};

log_trace 'Processing standard input';

my $parser = new MIME::Parser;
$parser->output_dir($parser_tmp_dir);
$parser->output_prefix('meqaris-msg');
my $entity = $parser->parse(\*STDIN);
if ( ! $entity ) {

	log_fatal 'Standard input parsing failed: ' . $parser->last_error;
	dbunlock();
	exit 4;
}

log_trace 'Standard input processed';

my @parts = $entity->parts;

my $sender = $entity->head->get('From', 0);
chomp $sender;
my $subject = $entity->head->get('Subject', 0);
chomp $subject;
my $reply_sender = '';
my $response_type = '';

log_info "New message from '$sender' with subject '$subject'.";

my $res = $dbh->selectall_hashref (db_stmt ($dbh, 
	'select r_id, r_name, r_email, r_enabled from meqaris.meq_resources'), 'r_email');
if ( ! $res ) {

	log_warn 'Cannot read resources from the database - all invitations will be silently ignored.'
		. " Database error:\n" . $dbh->errstr;
}

my $processed = 0;
my $dberr = undef;
my %replied_attenddees = ();

log_trace 'Processing message parts.';

for my $p (@parts) {

	if ( $p->mime_type eq 'text/calendar'
		|| $p->mime_type eq 'application/ics' ) {

		log_info "Message from '$sender' with subject '$subject' contains an iCalendar attachment.";

		# bodyhandle() runs the decoding, as_string() doesn't
		my $orig_invitation = $p->bodyhandle->as_string;
		log_debug "Message from '$sender' with subject '$subject' contains this iCalendar:\n" .
			"-----\n$orig_invitation\n------";

		my $ical = Data::ICal->new(data => $orig_invitation);

		# Check the method property. ARRAY! List context required!
		my ($method) = prop_array($ical, 'METHOD');
		if ( val($method) ne 'REQUEST' and val($method) ne 'CANCEL' ) {

			log_warn " Message from '$sender' with subject '$subject'" .
				" contains an iCalendar, but its method '" .
				val($method) . "' is unsupported. Skipping.";
			next;
		}

		# Read the time zones, if any:
		log_trace 'Processing iCalendar time zones.';
		my (%vtz, @vevents);
		for my $entry (@{$ical->entries}) {

			my $type = $entry->ical_entry_type;
			if ( $type eq 'VEVENT' ) {

				push @vevents, $entry;
			}
			elsif ( $type eq 'VTIMEZONE' ) {

				my $dtz = DateTime::TimeZone::ICal->from_ical_entry($entry);
				$vtz{$dtz->name} = $dtz;
			}
		}
		log_trace 'Done processing iCalendar time zones. Processing events.';

		my @response_details = ();
		# Process events:
		EV: for my $event (@vevents) {

			Log::Log4perl::NDC->remove();
			if ( ! defined ($event->property('uid')) ) {

				log_warn 'Event contains no UID - ignoring.';
				next;
			}
			my ($uid) = prop_array($event, 'uid');
			Log::Log4perl::NDC->push(val ($uid));
			log_trace 'Got UID.';

			if ( ! defined ($event->property('attendee')) ) {

				log_info 'Event contains no attendees - ignoring.';
				next;
			}

			if ( ! defined ($event->property('organizer')) ) {

				log_warn 'Event contains no organizer - ignoring.';
				next;
			}
			my ($organizer) = prop_array($event, 'organizer');
			my $organizer_mail = undef;
			($organizer_mail = val($organizer)) =~ s/^mailto://io;

			log_info "Got event with organizer='" . val ($organizer) . "'";
			if ( val ($method) eq 'REQUEST' ) {

				log_trace 'The iCalendar is a REQUEST.';

				# Get the event properties. ARRAY! List context required!
				my ($dtstart) = prop_array ($event, 'dtstart');
				my ($dtend) = prop_array ($event, 'dtend');
				my ($duration) = prop_array ($event, 'duration');
				my ($dtstamp) = prop_array ($event, 'dtstamp');
				my ($sequence) = prop_array ($event, 'sequence');
				my ($summary) = prop_array ($event, 'summary');

				# recurring events:
				my ($exdates) = prop_array ($event, 'exdate');
				my ($exrules) = prop_array ($event, 'exrule');
				my ($rdates) = prop_array ($event, 'rdate');
				my ($rrules) = prop_array ($event, 'rrule');

				log_info "Event REQUEST: dtstart='" . val ($dtstart) .
					"', dtend='" . val ($dtend) .
					"', duration='" . val ($duration) .
					"', dtstamp='" . val ($dtstamp).
					"', sequence='" . val ($sequence) .
					"', summary='" . val ($summary) . "'.";

				log_trace 'Adding timezones to start date.';

				# Get the timezone ID from the parameters:
				my $tzid_start = $dtstart->parameters->{'TZID'};
				# Convert the dates to the timezone:
				my $dt_start_conv = DateTime::Format::ICal->parse_datetime(val($dtstart));

				# The datetime is changed to be 'floating':
				$dt_start_conv->set_time_zone($vtz{$tzid_start})
					if $tzid_start and $vtz{$tzid_start};

				my $dt_end_conv = undef;
				my $dates = undef;
				my @rdates_periods = ();
				my $dtstamp_conv = undef;
				if ( defined ($dtend) ) {

					log_trace 'Adding timezones to end date.';

					my $tzid_end = $dtend->parameters->{'TZID'};
					# Convert the dates to the timezone:
					$dt_end_conv = DateTime::Format::ICal->parse_datetime(val($dtend));
					# The datetime is changed to be 'floating':
					$dt_end_conv->set_time_zone($vtz{$tzid_end})
						if $tzid_end and $vtz{$tzid_end};
				}
				if ( defined ($dtstamp) ) {

					log_trace 'Adding timezones to date/timestamp.';

					my $tzid_end = $dtstamp->parameters->{'TZID'};
					# Convert the dates to the timezone:
					$dtstamp_conv = DateTime::Format::ICal->parse_datetime(val($dtstamp));
					# The datetime is changed to be 'floating':
					$dtstamp_conv->set_time_zone($vtz{$tzid_end})
						if $tzid_end and $vtz{$tzid_end};
				}
				if ( defined ($rdates) || defined ($rrules) ) {

					log_trace 'Processing RRULE.';

					$dates = DateTime::Set->empty_set;
					# add all the recursion rules and dates:
					if ( defined ($rrules) ) {
						for my $rrule ($rrules) {
							$dates = $dates->union (
								DateTime::Format::ICal->parse_recurrence (
									recurrence => val ($rrule),
									dtstart => $dt_start_conv)
								);
						}
					}
					log_trace 'Processing RDATE.';

					if ( defined ($rdates) ) {
						for my $rdate ($rdates) {
							my $rdate_val = val ($rdate);
							next unless $rdate_val;
							# Add only dates in RDATE here.
							# Adding periods changes everything
							# to DateTime::Span of zero length
							# and breaks the DateTime::Set
							if ( $rdate_val !~ m./.o ) {

								log_trace "Processing RDATE - date: '$rdate_val'.";

								# not a period - parse a date
								my $rdate_tzid = $rdate->parameters->{'TZID'};
								my $rdate_parsed = DateTime::Format::ICal->parse_datetime($rdate_val);

								$rdate_parsed->set_time_zone($vtz{$rdate_tzid})
									if $rdate_tzid and $vtz{$rdate_tzid};
								$dates = $dates->union (
									DateTime::Set->from_datetimes(
										dates => [$rdate_parsed]
									)
								);
							}
							else {
								push @rdates_periods, DateTime::Format::ICal->parse_period ($rdate_val);
							}
						}
					}
					log_trace 'Processing EXRULE.';

					# subtract all the exclusion rules and dates:
					if ( defined ($exrules) ) {
						for my $exrule ($exrules) {
							$dates = $dates->complement (
								DateTime::Format::ICal->parse_recurrence (
									recurrence => val ($exrule),
									dtstart => $dt_start_conv)
								);
						}
					}
					log_trace 'Processing EXDATE.';

					if ( defined ($exdates) ) {
						for my $exdate ($exdates) {
							my $exdate_tzid = $exdate->parameters->{'TZID'};
							my $exdate_parsed = DateTime::Format::ICal->parse_datetime(val ($exdate));

							$exdate_parsed->set_time_zone($vtz{$exdate_tzid})
								if $exdate_tzid and $vtz{$exdate_tzid};
							$dates = $dates->complement (
								DateTime::Set->from_datetimes(
									dates => [$exdate_parsed]
								)
							);
						}
					}
				}
				log_trace 'Matching attendees to events.';

				# Find an attendee matching one of our resources
				# and set the reply as ACCEPTED if the timeslot is free
				# (if the database allows the insert) and DECLINED
				# otherwise or on any error.
				AT: for my $attendee (@{$event->property('attendee')}) {

					# Get the raw e-mail address:
					($reply_sender = val($attendee)) =~ s/^mailto://io;
					# just one response per attendee:
					if ( defined ($replied_attenddees{$reply_sender}) ) {

						log_info "Participant '$reply_sender' already replied, skipping.";
						next;
					}
					if ( defined ($attendee->parameters->{'ROLE'})
						&& $attendee->parameters->{'ROLE'} eq 'NON-PARTICIPANT' ) {

						log_info "Participant '$reply_sender' is marked as NON-PARTICIPANT, skipping.";
						next;
					}
					# possible values for CUTYPE: no CUTYPE, GROUP, ROOM, RESOURCE
					@response_details = ();
					my $updating = 0;
					log_debug "Trying attendee '$reply_sender'.";
					# Skip if not one of our resources:
					if ( ! defined ($res->{$reply_sender}) ) {

						log_debug "Attendee '$reply_sender' doesn't match.";
						next;
					}
					log_debug "Attendee '$reply_sender' found";
					# check if resource is enabled:
					if ( $res->{$reply_sender}->{'r_enabled'} ) {

						log_debug "Attendee '$reply_sender' is enabled - proceeding.";

						# locking required for toc-tou problems with
						# invitations to multiple resources
						# start transaction for the lock + check + DELETE + INSERT
						$dbh->begin_work();
						$updating = 1;
						my $stmth = db_stmt ($dbh, 'lock table meqaris.meq_resource_reservations in access exclusive mode');
						if ( ! $stmth ) {
							log_warn "Database: cannot lock table.\n"
								. " Will continue, but concurrent booking of resources may give strange results.\n"
								. "Database error:\n"
								. $dbh->errstr;
						}
						$stmth->execute();
						if ( $dbh->errstr ) {
							log_warn "Database: cannot lock table.\n"
								. " Will continue, but concurrent booking of resources may give strange results.\n"
								. "Database error:\n"
								. $dbh->errstr;
						}
						log_trace 'Checking if an event with this UID already exists.';

						# Attendee found and enabled. Check if this is an update.
						my @values_find_del = (val ($uid), $reply_sender, defined ($dtstamp_conv)? $dtstamp_conv : undef);
						my @old_events = @{ $dbh->selectall_arrayref ( # NOTE: selectall_hashref seems not to work
							db_stmt ($dbh, 'select rr_id, rr_r_id, rr_organiser, coalesce (rr_seq, 0) as rr_seq, lower(rr_interval) as rr_dstart,'
								. ' rr_dtstamp, cast (coalesce ($3, \'19700101T010101Z\') as timestamp with time zone) as new_dtstamp'
								. ' from meqaris.meq_resource_reservations'
								. ' join meqaris.meq_resources on r_id = rr_r_id'
								. ' where rr_uid = $1 and r_email = $2'),
							{ Slice => {} }, @values_find_del) };
						for my $old_event (@old_events) {

							log_debug 'Event already exists - checking if this is an update.';

							# an update can imply changing a resource, so do not check resource ID!
							#if ( defined ($old_event->{'rr_r_id'})
							#	&& $res->{$reply_sender}->{'r_id'} == $old_event->{'rr_r_id'} ) {

								log_debug 'Old dtstamp=' . (defined ($old_event->{'rr_dtstamp'})?
									$old_event->{'rr_dtstamp'} : 'null');
								log_debug 'New dtstamp=' . (defined ($old_event->{'new_dtstamp'})?
									$old_event->{'new_dtstamp'} : 'null');

								# best to compare DB strings, otherwise e.g. '2022-05-19 17:31:52+02' vs. '20220519T153152Z'
								if ( defined ($old_event->{'rr_dtstamp'})
									&& defined ($old_event->{'new_dtstamp'})
									&& $old_event->{'rr_dtstamp'} ge $old_event->{'new_dtstamp'} ) {

									log_debug 'Same or earlier DTSTAMP - this is a re-send or a duplicate, NOT an update.' .
										' Can be an invitation sent to multiple resources. Skipping.';
									next EV;
								}

								log_debug 'Old seq=' . (defined ($old_event->{'rr_seq'})? $old_event->{'rr_seq'} : 'null');
								log_debug 'New seq=' . (defined ($sequence)? val ($sequence) : 'undef');
								if ( defined ($old_event->{'rr_seq'})
									&& defined ($sequence)
									&& int(val ($sequence)) <= $old_event->{'rr_seq'} ) {

									log_debug 'Same or earlier sequence number - this is a re-send or a duplicate, NOT an update.' .
										' Can be an invitation sent to multiple resources. Skipping.';
									next EV;
								}
							#}

							log_info 'This event is an update.';
							# check if the sender is the organizer and decline if not
							my $sender_mail;
							if ( $sender =~ /<([^>]+)>/o ) {
								$sender_mail = $1;
							}
							else {
								$sender_mail = $sender;
							}
							log_trace "Checking sender vs. event's organizer.";
							if ( $sender_mail ne $organizer_mail ) {

								log_info "Sender '$sender_mail' is not event's organizer '$organizer_mail' - declining.";
								$dbh->rollback if $updating;
								$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
								push @response_details, 'Declined for security reasons.';
								push @response_details, 'You are not the organizer of the attached event.';
								push @response_details, "The attached event's organizer is '$organizer_mail'.";
								$processed = send_mail ($event, $reply_sender,
									'Declined', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT;
							}
							log_debug "Sender '$sender_mail' is event's organizer '$organizer_mail'.";
							$old_event->{'rr_organiser'} =~ s/^mailto://io;
							if ( $sender_mail ne $old_event->{'rr_organiser'} ) {

								log_info "Sender '$sender_mail' is not the same organizer as in the database '" .
									$old_event->{'rr_organiser'} . "' - declining.";
								$dbh->rollback if $updating;
								$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
								push @response_details, 'Declined for security reasons.';
								push @response_details, 'You are not the organizer of the updated event.';
								push @response_details, "The updated event's original organizer is '$organizer_mail'.";
								$processed = send_mail ($event, $reply_sender,
									'Declined', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT;
							}
							log_debug "Sender '$sender_mail' is the same organizer as in the database. Deleting old event.";
							$dberr = undef;
							my $stmth = db_stmt ($dbh,
								'delete from meqaris.meq_resource_reservations ' .
								'where rr_id = $1');
							if ( $stmth ) {
								@values_find_del = ($old_event->{'rr_id'});
								$stmth->execute(@values_find_del);
								if ( $dbh->errstr ) {

									$dberr = $dbh->errstr;
								}
								else {
									log_info 'Old event deleted successfully.';
									last; # go insert the new one
								}
							}
							else {
								$dberr = $dbh->errstr;
							}
							if ( $dberr ) {
								log_error "Error deleting old event:\n" .
									$dberr;
								$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
								push @response_details, 'Unable to delete the event being updated.';
								push @response_details, "The database error is:\n" . $dberr;
								$dbh->rollback if $updating;
								$processed = send_mail ($event, $reply_sender,
									'Declined', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT;
							}
						} # for my $old_event (@old_events)
						# Try to insert the new event:
						log_info 'Inserting new event';
						$stmth = undef;
						my @values_insert = undef;
						if ( defined ($rdates) || defined ($rrules) ) {

							log_trace 'RDATE or RRULE defined - building recurring events.';

							#if ( ! defined ($dates->count()) ) { # works only for DateTime::Set
							if ( $dates->max->isa('DateTime::Infinite::Future') ) { # works for DateTime::Set and DateTime::SpanSet (for periods)

								log_info 'The event has an infinite number of occurrences - declining.';
								$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
								push @response_details, 'The event has an infinite number of occurrences. Please specify either a finite number of occurrences or an ending date.';
								$dbh->rollback if $updating;
								$processed = send_mail ($event, $reply_sender,
									'Declined', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT; # decline by each attendee
							}
							# start transaction for the INSERTs if not in one already
							$dbh->begin_work() if ! $updating;
							$updating = 1;
							my $was_err = 0;

							# process periods in RDATE separately,
							# otherwise it breaks the DateTime::Set
							for my $period (@rdates_periods) {
								next unless $period;
								log_trace 'Recurring event - RDATE period - inserting intervals.';

								$stmth = db_stmt ($dbh,
									'insert into meqaris.meq_resource_reservations ' .
									'(rr_r_id, rr_interval, rr_organiser, rr_summary, rr_dtstamp, rr_uid, rr_seq, rr_data) ' .
									'values ($1, $2, $3, $4, $5, $6, $7, $8)');
								@values_insert = (
									$res->{$reply_sender}->{'r_id'},
									'[' . $period->start . ',' . $period->end . ')',
									substr ($organizer_mail, 0, 1000),
									substr (val ($summary), 0, 1000),
									$dtstamp_conv,
									substr (val ($uid), 0, 1000),
									(defined ($sequence))? $sequence->value : 0,
									encode_base64 ($orig_invitation)
									);
								if ( $stmth ) {

									$stmth->execute(@values_insert);
									if ( $dbh->errstr ) {

										log_error "Error inserting new recurring event:\n" .
											$dbh->errstr . "\n - declining.";
										# DB error - decline
										$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
										push @response_details, 'Unable to insert the event.';
										if ( $dbh->errstr =~ /rr_interval_excl/o ) {
											push @response_details, 'The resource is booked in the selected period.';
										}
										elsif ( $dbh->errstr =~ /rr_interval_in_future/o ) {
											push @response_details, 'You tried to create an event in the past.';
										}
										push @response_details, "The database error is:\n" . $dbh->errstr;
										$dbh->rollback if $updating;
										$processed = send_mail ($event, $reply_sender,
											'Declined', \@response_details);

										$replied_attenddees{$reply_sender} = 1;
										$was_err = 1;
										last;
									}
								}
								else {
									log_error "Error inserting new recurring event:\n" .
										$dbh->errstr . "\n- declining.";
									# DB error - decline
									$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
									push @response_details, 'Unable to insert the event.';
									push @response_details, "The database error is:\n" . $dbh->errstr;
									$dbh->rollback if $updating;
									$processed = send_mail ($event, $reply_sender,
										'Declined', \@response_details);

									$replied_attenddees{$reply_sender} = 1;
									$was_err = 1;
									last;
								}
							}
							if ( ! $was_err ) {
								my $iter = $dates->iterator;
								while ( my $dt = $iter->next ) {

									if ( defined ($dt_end_conv) ) {

										log_trace 'Recurring event - DTEND defined - inserting intervals.';

										# calculate each end by adding the
										# event's interval 'dtend - dtstart'
										# to each event occurrence's start
										$stmth = db_stmt ($dbh,
											'insert into meqaris.meq_resource_reservations ' .
											'(rr_r_id, rr_interval, rr_organiser, rr_summary, rr_dtstamp, rr_uid, rr_seq, rr_data) ' .
											'values ($1, (select cast (\'[\' || $2 || \',\' || (select cast ($2 as timestamp with time zone) + (cast ($4 as timestamp with time zone) - cast ($3 as timestamp with time zone))) || \')\' as tstzrange)), ' .
											'$5, $6, $7, $8, $9, $10)');
										@values_insert = (
											$res->{$reply_sender}->{'r_id'},
											$dt->stringify,
											$dt_start_conv,
											$dt_end_conv,
											substr ($organizer_mail, 0, 1000),
											substr (val ($summary), 0, 1000),
											$dtstamp_conv,
											substr (val ($uid), 0, 1000),
											(defined ($sequence))? $sequence->value : 0,
											encode_base64 ($orig_invitation)
											);
									}
									elsif ( defined ($duration) ) {

										log_trace 'Recurring event - DTEND not defined but DURATION defined - building intervals using DURATION.';

										$stmth = db_stmt ($dbh,
											'insert into meqaris.meq_resource_reservations ' .
											'(rr_r_id, rr_interval, rr_organiser, rr_summary, rr_dtstamp, rr_uid, rr_seq, rr_data) ' .
											'values ($1, (select cast (\'[\' || $2 || \',\' || (select cast ($2 as timestamp with time zone) + cast ($3 as interval)) || \')\' as tstzrange)), ' .
											'$4, $5, $6, $7, $8, $9)');
										@values_insert = (
											$res->{$reply_sender}->{'r_id'},
											$dt->stringify,
											val ($duration),
											substr ($organizer_mail, 0, 1000),
											substr (val ($summary), 0, 1000),
											$dtstamp_conv,
											substr (val ($uid), 0, 1000),
											(defined ($sequence))? $sequence->value : 0,
											encode_base64 ($orig_invitation)
											);
									}
									else {
										log_info 'Neither DTEND nor DURATION defined in a recurring event. Declining anniversary-type event or an event that takes no time.';

										$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
										push @response_details, 'This event takes no time, so it needs no resources.';
										push @response_details, 'Neither DTEND nor DURATION specified.';
										$dbh->rollback if $updating;
										$processed = send_mail ($event, $reply_sender,
											'Declined', \@response_details);

										$replied_attenddees{$reply_sender} = 1;
										next AT;
									}
									if ( $stmth ) {

										$stmth->execute(@values_insert);
										if ( $dbh->errstr ) {

											log_error "Error inserting new recurring event:\n" .
												$dbh->errstr . "\n - declining.";
											# DB error - decline
											$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
											push @response_details, 'Unable to insert the event.';
											if ( $dbh->errstr =~ /rr_interval_excl/o ) {
												push @response_details, 'The resource is booked in the selected period.';
											}
											elsif ( $dbh->errstr =~ /rr_interval_in_future/o ) {
												push @response_details, 'You tried to create an event in the past.';
											}
											push @response_details, "The database error is:\n" . $dbh->errstr;
											$dbh->rollback if $updating;
											$processed = send_mail ($event, $reply_sender,
												'Declined', \@response_details);

											$replied_attenddees{$reply_sender} = 1;
											$was_err = 1;
											last;
										}
									}
									else {
										log_error "Error inserting new recurring event:\n" .
											$dbh->errstr . "\n- declining.";
										# DB error - decline
										$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
										push @response_details, 'Unable to insert the event.';
										push @response_details, "The database error is:\n" . $dbh->errstr;
										$dbh->rollback if $updating;
										$processed = send_mail ($event, $reply_sender,
											'Declined', \@response_details);

										$replied_attenddees{$reply_sender} = 1;
										$was_err = 1;
										last;
									}
								}
							}
							if ( ! $was_err ) {

								log_info "Inserted recurring event for attendee '$reply_sender'.";

								$attendee->parameters->{'PARTSTAT'} = 'ACCEPTED';
								$dbh->commit if $updating;
								$processed = send_mail ($event, $reply_sender,
									'Accepted', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
							}
							# NOTE: recurring event inserted - do not proceed with the normal!
							next AT;
						}
						elsif ( defined ($dt_end_conv) ) {

							log_trace 'DTEND defined - inserting interval directly.';

							$stmth = db_stmt ($dbh,
								'insert into meqaris.meq_resource_reservations ' .
								'(rr_r_id, rr_interval, rr_organiser, rr_summary, rr_dtstamp, rr_uid, rr_seq, rr_data) ' .
								'values ($1, $2, $3, $4, $5, $6, $7, $8)');
							@values_insert = (
								$res->{$reply_sender}->{'r_id'},
								"[$dt_start_conv, $dt_end_conv)",
								substr ($organizer_mail, 0, 1000),
								substr (val ($summary), 0, 1000),
								$dtstamp_conv,
								substr (val ($uid), 0, 1000),
								(defined ($sequence))? $sequence->value : 0,
								encode_base64 ($orig_invitation)
								);
						}
						elsif ( defined ($duration) ) {

							log_trace 'DTEND not defined but DURATION defined - building interval using DURATION.';

							$stmth = db_stmt ($dbh,
								'insert into meqaris.meq_resource_reservations ' .
								'(rr_r_id, rr_interval, rr_organiser, rr_summary, rr_dtstamp, rr_uid, rr_seq, rr_data) ' .
								'values ($1, (select cast (\'[\' || $2 || \',\' || (select cast ($2 as timestamp with time zone) + cast ($3 as interval)) || \')\' as tstzrange)), ' .
								'$4, $5, $6, $7, $8, $9)');
							@values_insert = (
								$res->{$reply_sender}->{'r_id'},
								$dt_start_conv,
								val ($duration),
								substr ($organizer_mail, 0, 1000),
								substr (val ($summary), 0, 1000),
								$dtstamp_conv,
								substr (val ($uid), 0, 1000),
								(defined ($sequence))? $sequence->value : 0,
								encode_base64 ($orig_invitation)
								);
						}
						else {
							log_info 'None of: DTEND, DURATION, RDATE or RRULE defined. Declining anniversary-type event or an event that takes no time.';

							$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
							push @response_details, 'This event takes no time, so it needs no resources.';
							push @response_details, 'None of: DTEND, DURATION, RDATE or RRULE specified.';
							$dbh->rollback if $updating;
							$processed = send_mail ($event, $reply_sender,
								'Declined', \@response_details);

							$replied_attenddees{$reply_sender} = 1;
							next AT;
						}
						if ( $stmth ) {

							$stmth->execute(@values_insert);
							if ( $dbh->errstr ) {

								log_error "Error inserting new event:\n" .
									$dbh->errstr . "\n- declining.";
								# DB error - decline
								$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
								push @response_details, 'Unable to insert the event.';
								if ( $dbh->errstr =~ /rr_interval_excl/o ) {
									push @response_details, 'The resource is booked in the selected period.';
								}
								elsif ( $dbh->errstr =~ /rr_interval_in_future/o ) {
									push @response_details, 'You tried to create an event in the past.';
								}
								push @response_details, "The database error is:\n" . $dbh->errstr;
								$dbh->rollback if $updating;
								$processed = send_mail ($event, $reply_sender,
									'Declined', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT;
							}
							else {
								log_info "Inserted for attendee '$reply_sender'.";
								$attendee->parameters->{'PARTSTAT'} = 'ACCEPTED';
								$dbh->commit if $updating;
								$processed = send_mail ($event, $reply_sender,
									'Accepted', \@response_details);

								$replied_attenddees{$reply_sender} = 1;
								next AT;
							}
						}
						else {
							log_error "Error inserting new event:\n" .
								$dbh->errstr . "\n- declining.";
							# DB error - decline
							$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
							push @response_details, 'Unable to insert the event.';
							push @response_details, "The database error is:\n" . $dbh->errstr;
							$dbh->rollback if $updating;
							$processed = send_mail ($event, $reply_sender,
								'Declined', \@response_details);

							$replied_attenddees{$reply_sender} = 1;
							next AT;
						}
					}
					else {
						log_info 'Resource disabled - declining.';
						# Attendee disabled - decline
						$attendee->parameters->{'PARTSTAT'} = 'DECLINED';
						push @response_details, 'The resource you selected has been disabled.';
						$dbh->rollback if $updating;
						$processed = send_mail ($event, $reply_sender,
							'Declined', \@response_details);
						$replied_attenddees{$reply_sender} = 1;
						next AT;
					}
				} # for my $attendee
			} # 'REQUEST'
			elsif ( val ($method) eq 'CANCEL' ) {

				log_info 'The iCalendar is a CANCEL.';

				# Cancellation request - check if the event
				# exists, check if sender is the organiser
				# and if the organiser in the cancellation
				# request is the organiser of the original event
				if ( ! defined ($uid) ) {

					# should never be entered
					log_warn 'Event UID not defined, cannot delete - ignoring.';
					next;
				}
				log_trace 'Comparing sender and organizer e-mail addresses.';
				my $sender_mail;
				if ( $sender =~ /<([^>]+)>/o ) {
					$sender_mail = $1;
				}
				else {
					$sender_mail = $sender;
				}
				if ( $sender_mail ne $organizer_mail ) {

					log_info "Sender '$sender_mail' is not event's organizer '$organizer_mail' - ignoring.";
					next;
				}
				log_debug "Sender '$sender_mail' is event's organizer '$organizer_mail'.";
				my @values_cancel = (val ($uid));
				log_trace 'Searching the database for the event to cancel.';
				my @events = @{ $dbh->selectall_arrayref ( # NOTE: selectall_hashref seems NOT to work
					db_stmt ($dbh, 'select rr_id, rr_organiser from meqaris.meq_resource_reservations where rr_uid = $1'),
					{ Slice => {} }, @values_cancel) };
				if (! defined ($events[0]{'rr_id'}) ) {

					log_info 'Event not found in database - ignoring.';
					next;
				}
				log_trace 'Event found in the database.';
				$events[0]{'rr_organiser'} =~ s/^mailto://io;
				if ( $sender_mail ne $events[0]{'rr_organiser'} ) {

					log_info "Sender '$sender_mail' is not the same organizer as in the database '" .
						$events[0]{'rr_organiser'} . "' - ignoring.";
					next;
				}
				log_trace 'Event sender is the organizer. Deleting the event.';
				my $sql = 'delete from meqaris.meq_resource_reservations where rr_uid = $1';
				@values_cancel = (val ($uid));
				my ($recurr_id) = prop_array ($event, 'RECURRENCE-ID');
				if ( defined ($recurr_id) ) {

					# add the recurrence ID of a single instance to delete
					$sql .= ' and lower(rr_interval) = $2';
					push @values_cancel, val ($recurr_id);
				}
				my $stmth = db_stmt ($dbh, $sql);
				if ( $stmth ) {
					$stmth->execute(@values_cancel);
					if ( $dbh->errstr ) {
						log_error "Error deleting event:\n" .
							$dbh->errstr;
					}
					else {
						log_info 'Event deleted successfully.';
					}
				}
				else {
					log_error "Error deleting event:\n" .
						$dbh->errstr;
				}
			} # 'CANCEL'
		} # event loop
	} # $p->mime_type eq 'text/calendar' || $p->mime_type eq 'application/ics'
	# Process just one invitation per each e-mail. Various mail clients
	# may send the same invitation using different MIME types, so reply
	# to just the first one successfully processed and stop processing
	# to avoid conflicts and unnecessary meeting rejections.
	last if $processed;
}

$dbh->disconnect;
$parser->filer->purge;
dbunlock();

exit 0;

# ============================ Subroutines: ===================================

# ============================ print_help ===================================

sub print_help {

	print
		"Meqaris (Meeting Equipment and Room Invitation System)\n" .
			" - a system that allows booking meeting rooms and other resources\n" .
			" using e-mail invitations. See https://meqaris.sourceforge.io/\n" .
			"Author: Bogdan 'bogdro' Drozdowski, bogdro (at) users . sourceforge . net.\n\n" .
			"Syntax: meqaris [options]\n\n" .
			"Options:\n" .
			"--conf <file>\t\t\tUse the given configuration file\n" .
			"--create <name>\t\t\tCreate a new meeting resource\n" .
			"--delete <name>\t\t\tDelete a meeting resource with the given name\n" .
			"--description <desc>\t\tSpecifies a description for a meeting resource when creating or updating\n" .
			"--destroy-db\t\t\tDestroys the Meqaris schema on the database\n" .
			"--disable <name>\t\tDisables the meeting resource with the given name\n" .
			"--email <e-mail address>\tSpecifies an e-mail address for the meeting resource when creating or updating\n" .
			"--enable <name>\t\t\tEnables the meeting resource with the given name\n" .
			"-h|--help|-?\t\t\tShows this help message\n" .
			"--license|licence\t\tShows the license information\n" .
			"--name <name>\t\t\tSpecifies a new name for the meeting resource when updating\n" .
			"--set <name>\t\t\tSpecifies the name of the configuration setting to modify\n" .
			"--status\t\t\tShows the operability status of Meqaris\n" .
			"--update <name>\t\t\tUpdate a meeting resource with the given name\n" .
			"--upgrade-db\t\t\tRun the necessary Meqaris database structure upgrade scripts\n" .
			"--value <value>\t\t\tSpecifies the new value of the configuration setting to modify using --set\n" .
			"--version\t\t\tDisplay Meqaris version information\n"
		;
}

# ============================ send_mail ===================================

sub send_mail ($$$@) {

	my $event = shift;
	my $from = shift;
	my $response_type = shift;
	my $response_details = shift;

	log_debug "Preparing response '$response_type: $subject' from '$from' to '$sender'";

	my $from_full = "<$from>";
	if ( defined ($res) && defined ($res->{$from}) && defined ($res->{$from}->{'r_name'}) ) {

		$from_full = $res->{$from}->{'r_name'} . " <$from>";
	}

	my $calendar_reply = Data::ICal->new(rfc_strict => 1);
	$calendar_reply->add_entry($event);
	$calendar_reply->add_property('PRODID', "Meqaris $meqaris_version - https://meqaris.sourceforge.io/");
	$calendar_reply->add_property('METHOD', 'REPLY');

	my @mail_lines = ("$response_type: $subject\n");
	my $reply_detail_level = $dbcfg->{'reply_detail_level'}->{'c_value'};
	if ( ! is_empty ($reply_detail_level) ) {
		for (my $i = 0; $i < int($reply_detail_level); $i++) {
			push @mail_lines, $response_details->[$i] . "\n" if defined ($response_details->[$i]);
		}
	}
	push @mail_lines, ("-- \n", "Generated by Meqaris version $meqaris_version\n", "https://meqaris.sourceforge.io/\n");
	my $mail = MIME::Entity->build (
		#Type => 'multipart/mixed', # set automatically
		From => $from_full,
		To => $sender,
		Subject => substr ("$response_type: $subject", 0, 78), # length - rfc2822
		Encoding => 'quoted-printable',
		Data => \@mail_lines
	);
	$mail->head->add('Comments', "Generated by Meqaris version $meqaris_version - https://meqaris.sourceforge.io/");
	$mail->attach(
		Data => $calendar_reply->as_string,
		Type => 'text/calendar; method=REPLY',
		Encoding => 'base64'
	);
	log_debug "Response mail:\n=====\n" . $mail->stringify . "\n=====";
	my $mail_sending_method = $dbcfg->{'mail_sending_method'}->{'c_value'};
	if ( defined ($mail_sending_method) && $mail_sending_method eq 'mail_command' ) {

		# send by piping the mail to a command
		my $mail_command = $dbcfg->{'mail_command'}->{'c_value'};
		if ( ! is_empty ($mail_command) ) {

			# remove any pipe symbols first:
			$mail_command =~ s/^\s*\|\s*//o;
			log_info "Sending response to '$sender' using command '$mail_command'";
			open (my $cmd, '|-', $mail_command);
			if ( ! $cmd ) {
				log_warn "Mail_sending_method = mail_command, but the command failed to run: $!." .
					' Cannot send the response.';
			}
			print $cmd $mail->stringify;
			close $cmd;
			return 1;
		}
		else {
			log_warn 'Mail_sending_method = mail_command, but mail_command was not defined.' .
				' Cannot send the response.';
		}
	}
	else {
		# send by mail (the default)
		my %send_cfg = (
			MailFrom => $from_full,
			To => $sender);
		my $mail_server = $dbcfg->{'mail_server'}->{'c_value'};
		my $mail_server_port = $dbcfg->{'mail_server_port'}->{'c_value'};
		$send_cfg{Host} = $mail_server if ! is_empty ($mail_server);
		$send_cfg{Port} = $mail_server_port if ! is_empty ($mail_server_port);
		$send_cfg{Debug} = 1 if defined ($logger) && $logger->is_trace (); # prints to screen

		log_info "Sending response to '$sender' using " .
			"mail_server:port='" .
			(defined ($mail_server)? $mail_server : '(default)') .
			':' . (defined ($mail_server_port)? $mail_server_port : '(default port)') ."'";
		$mail->smtpsend(%send_cfg);
		return 1;
	}
	return 0;
}

# ============================ val ===================================

sub val ($) {

	my $prop = shift;
	return (defined ($prop)? $prop->value : '(undef)');
}

# ============================ clean_name ===================================

sub clean_name ($) {

	$name = shift;
	$name =~ s/[^a-z0-9_.\-@\/\\#$%&*()\[\];',]/_/gi;
	return $name;
}

# ============================ prop_array ===================================

sub prop_array ($$) {

	my $event = shift;
	my $prop = shift;
	# Check the property. ARRAY! List context required!
	return (defined ($event->property($prop))? @{$event->property($prop)} : undef);
}

# ============================ is_empty ===================================

sub is_empty ($) {

	my $v = shift;
	return ((defined ($v) && $v ne '')? 0 : 1);
}

# ============================ db_connect ===================================

sub db_connect {

	my $dbtype = $inicfg{'meqaris.dbtype'};
	if ( ! defined($dbtype) ) {
		log_fatal 'Database: cannot connect: missing "dbtype" parameter';
		return undef;
	}
	my $conn_timeout = 30;
	$conn_timeout = $inicfg{"$dbtype.connect_timeout"} unless is_empty ($inicfg{"$dbtype.connect_timeout"});

	my $dbh = DBI->connect(
		'dbi:Pg:dbname=' . $inicfg{"$dbtype.dbname"} .
		';host=' . $inicfg{"$dbtype.host"} .
		';port=' . $inicfg{"$dbtype.port"} .
		';connect_timeout=' . $conn_timeout,
		$inicfg{"$dbtype.username"},
		$inicfg{"$dbtype.password"},
		{AutoCommit => 1, RaiseError => 0, PrintError => 0});
	if ( ! $dbh ) {
		log_fatal 'Database: cannot connect: ' . $DBI::errstr;
	}
	return $dbh;
}

sub db_connect_print {

	my $dbh = db_connect();
	if ( ! $dbh ) {

		print 'Database: cannot connect: ' . $DBI::errstr . "\n";
		exit 2;
	}
	return $dbh;
}

# ============================ db_stmt ===================================

sub db_stmt ($$) {

	my $dbh = shift;
	my $sql = shift;
	my $stmth = $dbh->prepare ($sql);
	if ( ! $stmth ) {
		log_fatal 'Database: cannot create statement: ' . $dbh->errstr;
	}
	return $stmth;
}

sub db_stmt_print ($$) {

	my $dbh = shift;
	my $sql = shift;
	my $stmth = db_stmt($dbh, $sql);
	if ( ! $stmth ) {

		print "Database: cannot create statement. See the logs.\n";
		exit 2;
	}
	return $stmth;
}

# ============================ db_version ===================================

sub db_version ($) {

	my $dbh = shift;
	my $stmth = db_stmt ($dbh, "select c_value from meqaris.meq_config where c_name = 'db_version'");
	return 0 unless $stmth;
	$stmth->execute();
	if ( $dbh->errstr ) {
		# this CAN be a normal situation, e.g. on first install
		log_debug 'Database: cannot get version: ' . $dbh->errstr;
		return 0;
	}
	my $db_ver = $stmth->fetchrow_arrayref;
	return int($db_ver->[0]);
}

# ============================ db_run_file ===================================

sub db_run_file ($$) {

	my $dbh = shift;
	my $filename = shift;

	my $f = undef;
	if ( ! open ($f, '<', $filename) ) {

		log_fatal "Database upgrade: cannot open file '$filename': $!";
		print "Database upgrade: cannot open file '$filename': $!". "\n";
		return 0;
	}
	my @lines = <$f>;
	close $f;

	my $stmth = db_stmt ($dbh, join ("\n", @lines));
	return 0 unless $stmth;
	$stmth->execute();
	if ( $dbh->errstr ) {

		log_fatal "Database upgrade: cannot execute file '$filename': " . $dbh->errstr;
		print "Database upgrade: cannot execute file '$filename': " . $dbh->errstr . "\n";
		return 0;
	}
	return 1;
}

# ============================ db_upgrade ===================================

sub db_upgrade ($) {

	my $dbh = shift;
	my $ret = dblock();
	return 0 unless $ret;
	$ret = 1;
	my $db_ver = db_version ($dbh);

	if ( $meqaris_dbversion != $db_ver ) {

		if ( $db_ver == 0 ) {

			# new database - just run the full script (in a transaction)
			$dbh->begin_work();
			if (! db_run_file ($dbh, "$inicfg{'meqaris.datadir'}/sql/meqaris-full.pgsql") ) {

				log_fatal "Can't update database version to $meqaris_dbversion.";
				$dbh->rollback();
				$ret = 0;
			}
			else {
				$dbh->commit();
			}
		}
		elsif ($db_ver < $meqaris_dbversion) {

			# run just the missing scripts, one at a time, each
			# in its own transaction
			for (my $i = $db_ver + 1; $i <= $meqaris_dbversion; $i++) {

				$dbh->begin_work();
				if (! db_run_file ($dbh, "$inicfg{'meqaris.datadir'}/sql/meqaris-v$i.pgsql") ) {

					log_fatal "Can't update database version from $db_ver to $meqaris_dbversion";
					$dbh->rollback();
					$ret = 0;
					last;
				}
				$dbh->commit();
			}
		}
		elsif ($db_ver > $meqaris_dbversion) {

			log_warn "Database version $db_ver is newer than the expected version $meqaris_dbversion." .
				' The application may behave improperly.' .
				' You can continue or leave and install the correct application version.';
			$ret = 1;
		}
	}
	dbunlock();
	return $ret;
}

sub db_upgrade_print ($) {

	my $dbh = shift;
	my $res = db_upgrade($dbh);
	if ( ! $res ) {

		print "Database: cannot upgrade. See the logs.\n";
		exit 3;
	}
	return $res;
}

# ============================ Locking ===================================

my $lock_filename = undef;

sub dblock () {

	my $lockdir = '/tmp';
	if ( defined ($inicfg{'meqaris.lock_dir'}) && -d $inicfg{'meqaris.lock_dir'} ) {
		$lockdir = $inicfg{'meqaris.lock_dir'};
	}
	else {
		log_warn "The lock directory '"
			. (defined ($inicfg{'meqaris.lock_dir'})? $inicfg{'meqaris.lock_dir'} : '?')
			. "' is not defined or is not a directory - using /tmp as the default.";
	}

	$lock_filename = "$lockdir/meqaris.lck";
	log_trace 'Trying to file-lock.';

	sysopen (my $lock_fd, $lock_filename, O_WRONLY | O_EXCL | O_CREAT);
	if ( ! defined ($lock_fd) ) {
		my $start_time = time;

		log_trace 'Waiting for lock';

		while ( time < $start_time + 30 ) {

			sysopen (my $lock_fd, $lock_filename, O_WRONLY | O_EXCL | O_CREAT);
			if ( defined ($lock_fd) ) {

				close $lock_fd; # the file itself is not needed, as long as it exists
				last;
			}
			sleep 1;
		}

		log_trace 'Waiting for lock finished';

		if ( time >= $start_time + 30 ) {

			log_fatal "Lock file exists or cannot be created: '$lock_filename'. Cannot continue.";
			return 0;
		}
	}
	return 1;
}

sub dbunlock () {

	unlink $lock_filename if defined ($lock_filename) and -f $lock_filename;
	$lock_filename = undef;
}

# ============================ Loggers: ===================================

sub log_trace ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_trace () ) {
		$logger->trace ($msg);
	}
	elsif ( $log_level > 5 ) {
		print STDERR "Meqaris: trace: $msg\n";
	}
}

sub log_debug ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_debug () ) {
		$logger->debug ($msg);
	}
	elsif ( $log_level > 4 ) {
		print STDERR "Meqaris: debug: $msg\n";
	}
}

sub log_info ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_info () ) {
		$logger->info ($msg);
	}
	elsif ( $log_level > 3 ) {
		print STDERR "Meqaris: info: $msg\n";
	}
}

sub log_warn ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_warn () ) {
		$logger->warn ($msg);
	}
	elsif ( $log_level > 2 ) {
		print STDERR "Meqaris: warning: $msg\n";
	}
}

sub log_error ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_error () ) {
		$logger->error ($msg);
	}
	elsif ( $log_level > 1 ) {
		print STDERR "Meqaris: error: $msg\n";
	}
}

sub log_fatal ($) {

	my $msg = shift;

	if ( defined ($logger) && $logger->is_fatal () ) {
		$logger->fatal ($msg);
	}
	elsif ( $log_level > 0 ) {
		print STDERR "Meqaris: fatal error: $msg\n";
	}
}
